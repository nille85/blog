group 'be.nille.blog'
version '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}


apply plugin: 'java'

dependencies {
    compile group: 'org.mongodb', name: 'mongo-java-driver', version: '3.3.0'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.6'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.6'
    //bridge logging from JCL to SLF4j
    compile 'org.slf4j:jcl-over-slf4j:1.7.12'
    //Logback
    compile 'ch.qos.logback:logback-classic:1.1.3'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}


sourceSets {
  test {
    java.srcDir 'src/test/unit/java'
    
  }
  integration {
    java.srcDir 'src/test/integration/java'
    resources.srcDir 'src/test/resources'
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
    
  }
}

configurations {
  integrationCompile.extendsFrom testCompile
  integrationRuntime.extendsFrom testRuntime
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDir = sourceSets.integration.output.classesDir
  classpath = sourceSets.integration.runtimeClasspath
}

//add logging to the console for tests, remember to first call "clean" task in order to see output
test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

integration {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

